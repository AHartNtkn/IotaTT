entrypoints Module, Exp ;

comment "--" ;
comment "{-" "-}" ;

layout "where", "let" ;
layout stop "in" ;
-- Do not use layout toplevel as it makes pExp fail!

Module.   Module ::= "module" AIdent "where" "{" [Imp] [Decl] "}" ;

Import.   Imp ::= "import" AIdent ;
separator Imp ";" ;

DeclDef.    Decl ::= AIdent ":" Exp "=" ExpWhere ;
separator   Decl ";" ;

Where.    ExpWhere ::= Exp "where" "{" [Decl] "}" ;
NoWhere.  ExpWhere ::= Exp ;

SLet.          Exp  ::= "let" "{" [Decl] "}" "in" Exp ;
SLam.          Exp1 ::= "\\" [OTele] "." Exp ;
SLami.         Exp1 ::= "/" [AIdent] "." Exp ;
SFun.          Exp1 ::= Exp2 "->" Exp1 ;
SPi.           Exp1 ::= [PTele] "->" Exp1 ;
SIPi.          Exp1 ::= [ITele] "->" Exp1 ;
SIota.         Exp1 ::= "i" PTele "." Exp1 ;
SId.           Exp2 ::= Exp2 "~" Exp3 ;
SRho.          Exp2 ::= "r(" Exp "." Exp ")" Exp2 "." Exp3 ;
SAppi.         Exp3 ::= Exp3 "-" Exp4 ;
SApp.          Exp3 ::= Exp3 Exp4 ;
SFst.          Exp4 ::= Exp4 ".1" ;
SSnd.          Exp4 ::= Exp4 ".2" ;
SPair.         Exp5 ::= "[" Exp "|" Exp "]" ;
SVar.          Exp5 ::= AIdent ;
SBeta.         Exp5 ::= "B" ;
SU.            Exp5 ::= "*" ;
SHole.         Exp5 ::= HoleIdent ;
coercions Exp 5 ;

POTele.   OTele ::= PTele ;
AOTele.   OTele ::= AIdent ;
terminator nonempty OTele "" ;

PTele.    PTele ::= "(" Exp ":" Exp ")" ;
ITele.    ITele ::= "{" Exp ":" Exp "}" ;
terminator nonempty PTele "" ;
terminator nonempty ITele "" ;

token AIdent ('_')|(letter)(letter|digit|'\''|'_')*|('!')(digit)* ;
separator AIdent "" ;

token HoleIdent '?' ;